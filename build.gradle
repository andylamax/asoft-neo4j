buildscript {
    ext {
        kotlin_version = '1.3.70'
        neo4j_version = "3.2.9"
        persist_version = "16.0.0"
        coroutines_version = "1.3.4"
        android_build_tools_version = "3.6.0"
    }

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$android_build_tools_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'kotlin-multiplatform' version '1.3.70'
}

group 'tz.co.asoft'
version '2.0.0'

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main {
            manifest.srcFile 'src/androidMain/AndroidManifest.xml'
            resources.srcDirs = ['src/androidMain/resources']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

repositories {
    google()
    mavenCentral()
    jcenter()
    maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url 'https://dl.bintray.com/kotlin/kotlin-js-wrappers' }
    maven { url 'https://dl.bintray.com/kotlinx/kotlinx' }
    maven { url "https://jitpack.io" }
}


kotlin {
    jvm() {
        compilations.all {
            tasks[compileKotlinTaskName].kotlinOptions {
                jvmTarget = "1.8"
            }
        }
    }

    android() {
        compilations.all {
            tasks[compileKotlinTaskName].kotlinOptions {
                jvmTarget = "1.8"
            }
        }
        publishLibraryVariants("release")
    }

    js() {
        compilations.all {
            tasks[compileKotlinTaskName].kotlinOptions {
                metaInfo = true
                sourceMap = true
                moduleKind = 'commonjs'
                main = "call"
            }
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                api "com.github.andylamax.asoft-persist:asoft-persist-metadata:$persist_version"
                api "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$coroutines_version"
            }
        }

        androidMain {
            dependencies {
                implementation kotlin('stdlib')
                api "com.github.andylamax.asoft-persist:asoft-persist-android:$persist_version"
                api "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
            }
        }

        jvmMain {
            dependencies {
                implementation kotlin('stdlib')
                api "com.github.andylamax.asoft-persist:asoft-persist-jvm:$persist_version"
                api "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
                api "org.neo4j:neo4j-ogm-core:$neo4j_version"
                api "org.neo4j:neo4j-ogm-api:$neo4j_version"
                api "org.neo4j:neo4j-ogm-http-driver:$neo4j_version"
            }
        }

        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
                api "com.github.andylamax.asoft-persist:asoft-persist-js:$persist_version"
                api "org.jetbrains.kotlinx:kotlinx-coroutines-core-js:$coroutines_version"
            }
        }
    }
}